buildscript {
  repositories {
      maven { url "http://dl.bintray.com/calmdev/gradle-plugins" }
      mavenLocal()
      jcenter()
  }

  dependencies {
    classpath 'me.champeau.gradle:jbake-gradle-plugin:0.3.0-alpha.1'
    classpath 'org.akhikhl.gretty:gretty:1.4.0'
  }
}

plugins {
    id 'com.craigburke.bower-installer' version '2.5.1'
}

apply plugin: 'me.champeau.jbake'
apply plugin: 'distribution'
apply from: 'gradle/environment.gradle'

repositories {
  mavenLocal()
  jcenter()
}

ext {
    foundationVersion = '5.5.3'
    highlightjsVersion = '9.6.0'
    jqueryVersion = '2.1.4'
    modernizrVersion = '2.8.3'
}

bower {
    installBase = "$jbake.srcDirName/assets/vendor"

    'foundation'(foundationVersion){
        source '**/*.css'
        source '**/*.js'

        excludes 'fastclick'
        excludes 'jquery-placeholder'
        excludes 'jquery.cookie'
    }

    'foundation-icon-fonts'(){
        source 'foundation-icons*'
    }

    'highlightjs'(highlightjsVersion){
        source 'highlight.pack.min.js'
        source 'styles/darcula.css'
        excludes 'default.css'
    }

    'jquery'(jqueryVersion) {
        source 'dist/jquery.js' >> 'jquery.js'
    }

    'modernizr'(modernizrVersion) {
        source 'modernizr.js'
    }
}

if ( env == "local" ){
  apply plugin: 'war'
  apply plugin: 'org.akhikhl.gretty'

  gretty {
    httpPort = config.server.port
    contextPath = config.server.contextPath
    extraResourceBases = [bake.output]
  }
}
else {
  apply from: 'gradle/publish.gradle'
}

jbake{
  version = "2.4.0"
  asciidoctorjVersion = "1.5.4.1"
  configuration['blog.title'] = "calmdevelopment"
  configuration['blog.description'] = "'A personal Blog. Mostly about tech stuff. groovy, gradle, asciidoctor, jbake and other interesting stuff that crosses my path...'"
  configuration['db.store'] = "local"
  configuration['db.path']= "build/cache"
  configuration['site.host'] = config.server.url
  configuration['render.tags'] = true
  configuration['site.contextPath'] = config.server.contextPath
  configuration['foundation.version'] = foundationVersion
  configuration['twitter.user'] = "@knarfancho"
  configuration['asciidoctor.attributes'] = [
    "imagesdir=${config.server.contextPath}img",
    "source-highlighter=highlight.js",
    "icons=font"
  ]
}

distributions {
  main {
    contents {
      from { bake.outputs }
    }
  }
}

distZip.dependsOn bake
bake.dependsOn bowerInstall
